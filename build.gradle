buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.3'
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

apply from:'jdee.gradle'

allprojects {
  group = 'org.oscim'
  version = '0.5.9-SNAPSHOT'

  ext.gdxVersion = "1.2.0"

  repositories {
    mavenCentral()
    maven { url 'http://repo.boundlessgeo.com/main' }
    //maven { url 'https://github.com/hjanetzek/maven-repo/raw/master/' }
    mavenLocal()
  }

  // create IntelliJ project settings - untested
  apply plugin: 'idea'
}

subprojects {
  // create eclipse  project settings
  apply plugin: 'eclipse'

  task copySettings(type: Copy) {
    from "$rootDir/eclipse"
    into ".settings"
    include '**/*'
  }

  tasks.withType(JavaCompile) { 
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7" 
  }

  eclipseProject.dependsOn copySettings

  // create emacs JDEE project settings
  // http://ignatyev-dev.blogspot.de/2013/07/gradle-projects-in-jdee.html
  apply from:'../jdee.gradle'
}

